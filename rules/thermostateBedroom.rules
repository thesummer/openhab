
rule "Bedroom default"
when
    System started
then
    logInfo("bedroomTempInitializing", "SetBR temperatures starting...")
    brTemperatureComfort.sendCommand(19.0)
    brTemperatureEco.sendCommand(16.0)
end

rule "Bedroom thermostate set state"
when
    Time cron "0 30 22 1/1 * ? *" or
    Time cron "0 30 6 1/1 * ? *"  or
    Item JanOnline changed
then
    val turnOnTime = now.withTimeAtStartOfDay.plusHours(22).plusMinutes(30)
    val turnOffTime = now.withTimeAtStartOfDay.plusHours(6).plusMinutes(30)

    logInfo("bedroomTempChangeState", "SetBRState starting...")

    if (JanOnline.state == OPEN && (now.isAfter(turnOnTime) || now.isBefore(turnOffTime)))
    {
        logInfo("bedroomTempChangeState", "Setting heater status to COMFORT")
        brHeaterState.sendCommand("COMFORT")
    }
    else
    {
        logInfo("bedroomTempChangeState", "Setting heater status to ECO")
        brHeaterState.sendCommand("ECO")
    }
    logInfo("bedroomTempChangeState", "SetBRState finished")
end

rule "Update current bedroom temperature"
when
    Item brSetpoint changed or
    Item brCurrentTemperature changed
then
    logInfo("brUpdateTemp", "Updating temperature display")
    brCurrentTemp.sendCommand(brCurrentTemperature.state.toString() + " (" + brSetpoint.state.toString() + ")")
end

rule "Bedroom heater set temperature"
when
    Item brHeaterState changed or 
    Item brTemperatureComfort changed or
    Item brTemperatureEco changed
then

    logInfo("brTemp", "Bedroom heater state set to " + brHeaterState.state)
    switch (brHeaterState.state)
    {
        case "MANUAL": 
        {
            logInfo("brTemp", "Set current set point to " + brTemperatureComfort.state.toString())
            
        }
        case "ECO":
        {
            logInfo("brTemp", "Set current set point to " + brTemperatureEco.state.toString())
            brSetpoint.sendCommand(brTemperatureEco.state.toString())
        }
        case "COMFORT":
        {
            logInfo("brTemp", "Set current set point to " + brTemperatureComfort.state.toString())
            brSetpoint.sendCommand(brTemperatureComfort.state.toString())
        }
    }
end
