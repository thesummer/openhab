
rule "Bedroom default"
when
    System started
then
    logInfo("bedroomTempInitializing", "SetBR temperatures starting...")
    bedroomTemperatureComfort.sendCommand(19.0)
    bedroomTemperatureEco.sendCommand(16.0)
end

rule "Bedroom thermostate set state"
when
    Time cron "0 30 22 1/1 * ? *" or
    Time cron "0 30 6 1/1 * ? *"  or
    Item JanOnline changed
then
    val turnOnTime = now.withTimeAtStartOfDay.plusHours(22).plusMinutes(30)
    val turnOffTime = now.withTimeAtStartOfDay.plusHours(6).plusMinutes(30)

    logInfo("bedroomTempChangeState", "SetBRState starting...")

    if (JanOnline.state == OPEN && (now.isAfter(turnOnTime) || now.isBefore(turnOffTime)))
    {
        logInfo("bedroomTempChangeState", "Setting heater status to COMFORT")
        brHeaterState.sendCommand("COMFORT")
    }
    else
    {
        logInfo("bedroomTempChangeState", "Setting heater status to ECO")
        brHeaterState.sendCommand("ECO")
    }
    logInfo("bedroomTempChangeState", "SetBRState finished")
end

rule "Update current bedroom temperature"
when
    Item brSetpoint changed or
    Item bedroom_thermostat_CurrentTemperature changed
then
    logInfo("brUpdateTemp", "Updating temperature display")
    brCurrentTemp.sendCommand(bedroom_thermostat_CurrentTemperature.state.toString() + " (" + brSetpoint.state.toString() + ")")
end

rule "Bedroom heater set temperature"
when
    Item brHeaterState changed or 
    Item bedroomTemperatureComfort changed or
    Item bedroomTemperatureEco changed
then

    logInfo("brTemp", "Bedroom heater state set to " + brHeaterState.state)
    switch (brHeaterState.state)
    {
        case "MANUAL": 
        {
            logInfo("brTemp", "Set current set point to " + bedroomTemperatureComfort.state.toString())
            
        }
        case "ECO":
        {
            logInfo("brTemp", "Set current set point to " + bedroomTemperatureEco.state.toString())
            brSetpoint.sendCommand(bedroomTemperatureEco.state.toString())
        }
        case "COMFORT":
        {
            logInfo("brTemp", "Set current set point to " + bedroomTemperatureComfort.state.toString())
            brSetpoint.sendCommand(bedroomTemperatureComfort.state.toString())
        }
    }
end
