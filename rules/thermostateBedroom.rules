
rule "Bedroom default"
when
    System started
then
    bedroomTemperatureComfort.sendCommand(19.0)
    bedroomTemperatureEco.sendCommand(16.0)
end

rule "Bedroom thermostate set state"
when
    Time cron "0 30 22 1/1 * ? *" or
    Time cron "0 30 6 1/1 * ? *"  or
    Item JanOnline changed
then
    val turnOnTime = now.withTimeAtStartOfDay.plusHours(22).plusMinutes(30)
    val turnOffTime = now.withTimeAtStartOfDay.plusHours(6).plusMinutes(30)

    logInfo("bedroomTempChangeState", "SetBRState starting...")

    if (JanOnline.state == OPEN && (now.isAfter(turnOnTime) || now.isBefore(turnOffTime)))
    {
        logInfo("bedroomTempChangeState", "Setting heater status to ON")
        bedroomHeaterState.sendCommand(ON)
    }
    else
    {
        logInfo("bedroomTempChangeState", "Setting heater status to OFF")
        bedroomHeaterState.sendCommand(OFF)
    }
    logInfo("bedroomTempChangeState", "SetBRState finished")
end

rule "Bedroom heater set temperature"
when
    Item bedroomHeaterState changed or 
    Item bedroomTemperatureComfort changed or
    Item bedroomTemperatureEco changed
then
    if (bedroomHeaterState.state == ON)
    {
        logInfo("bedroomTemp", "Set current set point to " + bedroomTemperatureComfort.state.toString())
        bedroom_thermostat_SetpointTemperature.sendCommand(bedroomTemperatureComfort.state)
    }
    else
    {
        logInfo("bedroomTemp", "Set current set point to " + bedroomTemperatureEco.state.toString())
        bedroom_thermostat_SetpointTemperature.sendCommand(bedroomTemperatureEco.state)
    }
end
