var Timer disablePreheatTimer

rule "Startup"
when
    System started
then
   logInfo("LVStart", "Initializing Living Room Heater Rule")
   lrTemperatureComfort.sendCommand(23.0)
   lrTemperatureEco.sendCommand(16.0)
   logInfo("LVStart", "EcoTemp: " + lrTemperatureEco.state.toString())
   logInfo("LVStart", "ComfortTemp: " + lrTemperatureComfort.state.toString())
end

rule "Living room thermostate set state"
when
    Time cron "0 30 22 1/1 * ? *" or
    Time cron "0 45 8 1/1 * ? *"  or
    Item JanOnline changed or
    Item livingroomPreheat changed to OFF
then
    val turnOffTime = now.withTimeAtStartOfDay.plusHours(22).plusMinutes(30)
    val turnOnTime  = now.withTimeAtStartOfDay.plusHours(7).plusMinutes(30)

    if (JanOnline.state == OPEN && (now.isAfter(turnOnTime) && now.isBefore(turnOffTime)))
    {
        logInfo("LVHeaterStateChange", "Set to COMFORT (Jan " + JanOnline.state.toString())
        lrHeaterState.sendCommand("COMFORT")
    }
    else
    {
        logInfo("LVHeaterStateChange", "Set to ECO (Jan " + JanOnline.state.toString())
        lrHeaterState.sendCommand("ECO")
    }
end

rule "Living room enable preheat"
when 
    Item livingroomPreheat changed to ON
then
    lrHeaterState.sendCommand("COMFORT")
    logInfo("LVPreheatEnable", "Set heater to COMFORT")
    disablePreheatTimer = createTimer(now.plusHours(1),
                       [| livingroomPreheat.sendCommand(OFF)
                          logInfo("LVPreheatEnable", "Set preheat switch to OFF") ])
end

rule "Update current living room temperature"
when
    Item lrSetpoint changed or
    Item lrCurrentTemperature changed
then
    logInfo("lrUpdateTemp", "Updating temperature display")
    lrCurrentTemp.sendCommand(lrCurrentTemperature.state.toString() + " (" + lrSetpoint.state.toString() + ")")
end


rule "Living room heater set temperature"
when
    Item lrHeaterState changed or
    Item lrTemperatureComfort changed or
    Item lrTemperatureEco changed 
then
    logInfo("lrTemp", "Livingroom heater state set to " + lrHeaterState.state)
    if (lrSetpoint === null)
    {
        logInfo("lrTemp", "lrSetpoint is null")
    }
    switch (lrHeaterState.state)
    {
        case "MANUAL": 
        {
            logInfo("lrTemp", "Set current set point to " + lrTemperatureComfort.state.toString())
            
        }
        case "ECO":
        {
            logInfo("lrTemp", "Set current set point to " + lrTemperatureEco.state.toString())
            lrSetpoint.sendCommand(lrTemperatureEco.state.toString())
        }
        case "COMFORT":
        {
            logInfo("lrTemp", "Set current set point to " + lrTemperatureComfort.state.toString())
            lrSetpoint.sendCommand(lrTemperatureComfort.state.toString())
        }
    }
end
